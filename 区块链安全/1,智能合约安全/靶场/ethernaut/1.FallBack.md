
## 提示
通过这关你需要

1. 获得这个合约的所有权
2. 把他的余额减到0

  这可能有帮助

- 如何通过与ABI互动发送ether
- 如何在ABI之外发送ether
- 转换 wei/ether 单位 (参见 `help()` 命令)
- Fallback 方法


## 审计

### 主要关注
1. 资金
	1. 价格计算
2. 鉴权

### Code
![](media/Pasted%20image%2020250516014524.png)

```Solidty
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Fallback {
    // 记录各地址的贡献金额（以wei为单位）
    mapping(address => uint256) public contributions;
    // 记录合约当前所有者
    address public owner;

    // 构造函数：部署时将部署者设为所有者，并初始化其贡献为1000 ETH
    constructor() {
        owner = msg.sender;
        contributions[msg.sender] = 1000 * (1 ether); // 1000 ETH = 1000 * 10^18 wei
    }

    // 修饰器：仅允许所有者访问
    modifier onlyOwner() {
        require(msg.sender == owner, "caller is not the owner");
        _;
    }

    // 贡献函数：允许用户发送ETH（每次<0.001 ETH）
    // 若用户总贡献超过当前所有者，则成为新所有者
    function contribute() public payable {
        require(msg.value < 0.001 ether, "Contribution must be less than 0.001 ETH");
        contributions[msg.sender] += msg.value;
        if (contributions[msg.sender] > contributions[owner]) {
            owner = msg.sender; // 贡献超过当前owner时转移所有权
        }
    }

    // 查询调用者的贡献金额
    function getContribution() public view returns (uint256) {
        return contributions[msg.sender];
    }

    // 提款函数：仅所有者可调用，提取合约全部余额
    function withdraw() public onlyOwner {
        payable(owner).transfer(address(this).balance);
    }

    // 接收以太币的回退函数（当calldata为空时触发）
    // !!! 注意：若发送者已有贡献，发送任意金额ETH即可成为所有者
    receive() external payable {
        require(msg.value > 0, "Must send some ETH");
        require(contributions[msg.sender] > 0, "No prior contribution");
        owner = msg.sender; // 关键安全漏洞：允许通过直接转账夺取所有权
    }
}
```
### WP

![](media/Pasted%20image%2020250516014203.png)  

1. 先查看一下合约owner `await contract.owner()`![](media/Pasted%20image%2020250516011224.png)  
2. 为合约贡献少量以太币 `await contract.contribute({"value": toWei("0.0005")})`![](media/Pasted%20image%2020250516013438.png)  
3. 将 ether 发送到合约以触发接收函数:![](media/Pasted%20image%2020250516013620.png)  
4. 获知地址可用ether数![](media/Pasted%20image%2020250516013717.png)
5. 偷钱![](media/Pasted%20image%2020250516013823.png)![](media/Pasted%20image%2020250516013937.png)    
