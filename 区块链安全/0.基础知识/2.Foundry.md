# CEI
每个函数都应该遵循
CEI方法 ：检查（Checks）、状态变更（Effects）、交互（Interactions） CEI方法 是一种在Solidity（及智能合约开发中）广泛使用的设计模式，用于增强以太坊智能合约的安全性和可靠性。通过遵循 检查→状态变更→交互 的顺序，开发者可以显著减少漏洞风险，尤其是 重入攻击 和 状态篡改 。
# 安装到docker
dockfile
```bash
FROM ubuntu:latest

# 安装系统依赖
RUN apt-get update && \
    apt-get install -y \
    curl \
    build-essential \
    python3 \
    git \
    libssl-dev \
    pkg-config \
    && rm -rf /var/lib/apt/lists/*

# 安装Rust
RUN curl --proto '=https' --tlsv1.2 -sSf https://sh.rustup.rs | sh -s -- -y
ENV PATH="/root/.cargo/bin:${PATH}"

# 安装Foundry
RUN curl -L https://foundry.paradigm.xyz | bash
ENV PATH="/root/.foundry/bin:${PATH}"
RUN foundryup
```

```bash
docker build -t . 
```
# 初始化链
```bash
foundryup
anvil
```
# 创建智能合约并部署到链上

```bash
//**没有项目**
forge init
//**有项目**
forge build
//打开在 src 文件夹的SimpleStorage.sol 并且交互输入秘钥

forge create SimpleStorage --interactive

forge create SimpleStorage --rpc-url http://xxxx:xxx --private-key xxxx
```
```ad-important
永远不要把私钥以明文的形式出现，因此选择interactive
```
# 用来 部署智能合约的脚本
```c

import {Script} from "forge-std/Script.sol";
import {SimpleStorage} from "../src/SimpleStorage.sol";
//关键词Script
contract DeploySimpleStorage is Script {
    function run() external returns (SimpleStorage) {
    //链上广播
        vm.startBroadcast();

        SimpleStorage simpleStorage = new SimpleStorage();

        vm.stopBroadcast();
        return simpleStorage;
    }
}

```
```ad-tip
一般命名规则xxx.s.sol ，用来操作部署智能合约的脚本，在script文件夹
```
```ad-question
if not found foundry-std
remove lib/foundry-std
then 
forge install foundry-rs/forge-std --no-commit
```
```c
forge script script/DeploySimpleStorage.s.sol  --broadcast --private-key  0x2a871d0798f97d79848a013d4936a73bf4cc922c825d33c1cf7073dff6d409c6 --rpc-url http://127.0.0.1:8545

```
```ad-tip
往往存在一个.env 文件保存你的私钥 这是漏洞的一个点
对于你，除了开发使用，万万不可能在实际部署使用
```
```c
cast wallet import nameOfAccountGoesHere --interactive
```
![[Pasted image 20250227104822.png]]
```c
forge script script/DeploySimpleStorage.s.sol --rpc-url http://127.0.0.1:8545 --broadcast --account nameOfAccountGoesHere --sender  0xa0ee7a142d267c1f36714e4a8f75612f20a79720
```
然后输入你的密码，完全不需要输入私钥，非常好部署
# 交互部署后的合约
```c
cast send 0x5FbDB2315678afecb367f032d93F642f64180aa3 "store(uint256)" 1337 --rpc-url $RPC_URL --private-key $PRIVATE_KEY
cast call 0x5FbDB2315678afecb367f032d93F642f64180aa3 "retrieve()"
```
```ad-note
send 为remix橙色按钮，发送合约，会改变区块链
call 蓝色，就是view的关键词的函数
```
# 来到测试网
## 创建测试网节点
[alchemy](https://www.alchemy.com/)
![[Pasted image 20250227113214.png]]
创建一个节点
然后修改对应rpc-url 
metamask的私钥
## 验证你的合同
去往区块浏览器[sepolia scan](https://sepolia.etherscan.io/)
![[Pasted image 20250227175224.png]]
选择solidity 对应版本，上传你的源代码
## 一键美化代码格式
```ad-tip
forge fmt
```
# Setup foundry zksync
1. git clone https://github.com/matter-labs/foundry-zksync.git
2. cd foundry-zksync
3. cd foundryup-zksync
4. ./foundryup-zksync
## Setup nodejs
[download nodejs](https://nodejs.org/en/download)

## Run zksync chain
- one method
	 `anvil-zksync`
- another method
	1. `npx zksync-cli dev config`
	2. `npx zksync-cli dev start`(**must sure docker is running**)
## Compile with zksync
```c
forge build --zksync
```
## Deploy zksync
```c
forge create src/SimpleStorage.sol:SimpleStorage --rpc-url http://127.0.0.1:8011 --private-key 0x7726827caac94a7f9e1b160f7ea819f172f7b6f9d2a97f992c38edeab82d4110 --legacy --zksync
```
```ad-tip
zksync 还是比较弱智，所以需要我们完全明确合同位置

```
```ad-tip
--legacy is must 这个是允许旧版本交易的，一旦没有，很有可能产生无法和以前交易的区块
```
# Found me
## Start our repo.
>forge init 

运行一个测试
>forge test

## Start with src
### chainlink工具链
```ad-note
在foundry 以下导入会报错
import"@chainlink/contracts/src/v0.8/shared/interfaces/AggregatorV3Interface.sol";
你需要安装
forge install smartcontractkit/chainlink-brownie-contracts --no-commit
并在foundry.toml
remappings = ['@chainlink/contracts/ = lib/chainlink-brownie-contracts/contracts/']
```
# Test
```ad-summary
测试的流程
//安排操作，初始化 Arrange
//执行操作 Action
//断言   Assert

```
```c
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.18;

import {Test, console} from "forge-std/Test.sol";

import {FundMe} from "../src/FundMe.sol";

//声明为测试合同
contract FundMeTest is Test {

FundMe fundMe;

//测试最小美元为5

function setUp() external {

fundMe = new FundMe();

}

function testMinimumDollarIsFive() public {

assertEq(fundMe.MINIMUM_USD(), 5e18);

}

}
```
```ad-note
setup 函数是特殊的，它做了一些事，来确保测试环境和部署环境一样，重要的是它**优先**执行，且是extrenal
```
>then run.  
>forge test
>forge test -m simple_test_function
```ad-tip
console.log() 去找到对应变量
但需要forge test -vv 详细输出
```
## 本地获得汇率
>    AggregatorV3Interface(0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419).version();
>    
>    not work, so we can do something?
```c
forge test --fork-url sepolia_rpc_url
```
## 检查你对你的合同做了多少测试
```
forge coverage
forge coverage --report debug > coverage.txt
```
## 重构，更加模块化
```ad-question
因为我们可能在很多个chain做测试，修改源代码往往不太好
我们就需要在构造的同时给出对应chain所需要的外部合同地址
例如
AggregatorV3Interface
```
 ```c
 // Foudme.sol 在初始化合同给出参数
 constructor(address priceFeed){
    i_owner = msg.sender;
    s_priceFeed = AggregatorV3Interface(priceFeed);

}
```
## 进一步测试script
>往往我们测试的合同应该是script给出的，因为braodcast了

```c
//fundme.s.sol
function run() external returns (FundMe) {
    vm.startBroadcast();
    FundMe fundMe = new FundMe(0x694AA1769357215DE4FAC081bf1f309aDC325306);
    vm.stopBroadcast();
    return fundMe

}
```
```c
//fundme.t.sol
function setUp() external {
    deployFundMe = new DeployFundMe();
    fundMe = deployFundMe.run();

}
```
```ad-important
在 Forge 测试环境中，当你部署合约时，默认的 `msg.sender` 是测试合约本身（即 `address(this)`）。因此，构造函数中将 `owner` 设为 `msg.sender` 后，`owner` 的值自然等于测试合约的地址。：

### 原因分析

1. ​**测试执行上下文**：
    
    - 测试函数运行在测试合约（`TestContract`）内部。
    - 当你在测试中部署目标合约时，部署操作由测试合约发起，因此目标合约构造函数中的 `msg.sender` 就是测试合约的地址（`address(this)`）。
2. ​**默认行为**：
    
    - 未使用 `vm.prank` 或 `vm.startPrank` 时，所有交易（包括部署）的发送者均为测试合约本身。
```

```ad-important
在 Forge 测试中，当你在**测试脚本函数**（通常以 `Script` 结尾且包含 `run` 函数）中使用 `broadcast` 时，`msg.sender` 的行为会发生变化，导致 `owner` 等于实际交易发起者（如外部地址或指定账户）。以下是详细解释：

---

### 1. **默认行为回顾**
在普通测试函数中：
- 部署合约时，`msg.sender` 默认为**测试合约本身**（`address(this)`）。
- 因此，构造函数中 `owner = msg.sender` 会使得 `owner` 等于测试合约地址。

---

### 2. **`broadcast` 的作用**
在测试脚本函数中，`vm.broadcast()` 用于**模拟真实交易发起者**：
- 通过 `broadcast` 指定一个地址（如从私钥解析的地址），后续操作会以该地址作为 `msg.sender`。
- 如果未显式指定地址，`broadcast` 默认使用 `vm.envAddress("DEPLOYER")`（即环境变量中定义的部署者地址）。

---

### 3. **测试脚本中的 `owner` 为何等于 `msg.sender`**
当测试脚本函数包含 `broadcast` 时：
1. **启动广播**：  
   vm.startBroadcast(); // 默认使用部署者地址
   // 或显式指定地址
   vm.startBroadcast(deployerPrivateKey);

2. **部署合约**：  

   MyContract myContract = new MyContract(); // 此时 msg.sender = 广播地址

3. **结果**：  
   `owner` 会被设置为广播地址（而非测试合约地址），因为 `msg.sender` 已被 `broadcast` 覆盖。


| **场景**               | `msg.sender` 来源         | `owner` 值               |
|------------------------|--------------------------|--------------------------|
| 普通测试函数           | 测试合约（`address(this)`） | `address(this)`          |
| 测试脚本 + `broadcast` | 广播地址（如部署者地址）   | 广播地址（`msg.sender`） |

### 总结
在测试脚本中使用 `broadcast` 时，`msg.sender` 会被显式覆盖为广播地址（如部署者），因此 `owner` 等于该地址。这是 Forge 测试框架区分**普通测试**（模拟内部调用）和**脚本模式**（模拟真实链上交易）的核心机制。
```
## helpconfig
>为了更好兼容更多的链，或者说，不要让他每一次都去访问真实的链浪费gas
```c
//helpConfig.s.sol
import {Script} from "forge-std/Script.sol";

contract HelperConfig is Script {
    // If we are on a local Anvil, we deploy the mocks
    // Else, grab the existing address from the live network
    struct NetworkConfig {
    address priceFeed; // ETH/USD price feed address
}


    function activeNetworkConfig() public returns (NetworkConfig memory) {
        if (block.chainid == 31337) { 
            // 本地 Anvil 链的 chainid
            return getAnvilConfig();
        } else if (block.chainid == 11155111) { 
            // Sepolia 测试网的 chainid
            return getSepoliaConfig();
        } else {
            revert("Unsupported network");
        }
    }

function getSepoliaEthConfig() public pure returns (NetworkConfig memory) {
    NetworkConfig memory sepoliaConfig = NetworkConfig({
        priceFeed: 0x694AA1769357215DE4FAC081bf1f309aDC325306
    });
    return sepoliaConfig;
}

function getAnvilEthConfig() public pure returns (NetworkConfig memory) {}

}

```
```ad-tip
这里因为只返回一些链的信息，所以使用pure
BLOCK.ID 链的id
```
```c
// The next line runs before the vm.startBroadcast() is called
// This will not be deployed because the `real` signed txs are happening
// between the start and stop Broadcast lines.
HelperConfig helperConfig = new HelperConfig();

address ethUsdPriceFeed = helperConfig.activeNetworkConfig();

FundMe fundme = new FundMe(ethUsdPriceFeed);
```
### 本地模拟汇率合同
```c
import {MockV3Aggregator} from "../test/mocks/MockV3Aggregator.sol";

[...]

// In state variables section
MockV3Aggregator mockPriceFeed;

[...]

function getAnvilEthConfig() public returns (NetworkConfig memory) {
    vm.startBroadcast();
    mockPriceFeed = new MockV3Aggregator(8, 2000e8);
    vm.stopBroadcast();

    NetworkConfig memory anvilConfig = NetworkConfig({
        priceFeed: address(mockPriceFeed)
    });

    return anvilConfig;

}
```
## 预期的测试
```c
function testFundFailsWIthoutEnoughETH() public {
    vm.expectRevert(); // <- The next line after this one should revert! If not test fails.
    vm.expectRevert(Mycontract.Myerror.selector);
    fundMe.fund();     // <- We send 0 value

}
 ```
### 自定义预期报错
```c
vm.expectRevert(

abi.encodeWithSelector(Raffle.Raffle_notupkeep.selector,

balance, numplayers, uint256(state))

);
```
```ad-tip
我期待下面的报错，如果正常报错，说明测试正常，用来测试合约错误的作弊码
```
```ad-note
|​**断言方法**​|​**用途**​|​**核心要点**​|
|---|---|---|
|`vm.expectRevert`|验证调用是否回滚(即交易报错)|匹配自定义错误选择器或完整错误数据，仅对下一个调用生效。|
|`vm.expectEmit`|验证事件是否触发|严格匹配索引参数，非索引参数可用通配符，需先声明事件再执行操作。|
|`vm.expectCall`|验证合约间调用|精确匹配调用数据或使用通配符忽略参数，支持多次调用验证。|
```
### exceptEmit
```c
function expectEmit(
    bool checkTopic1,
    bool checkTopic2,
    bool checkTopic3,
    bool checkData,
    address emitter
) external;

event enterPlayer(address indexed player);
vm.expectEmit(true, false, false, false, address(raffle));
```
checktopic 1-3对应事件的索引的1-3参数，checkdata对应任何没有索引的参数

## 模拟用户
```c
address alice = makeAddr("alice");
//发钱给账户
vm.deal(alice, 1 ether);
```
```c
function testFundUpdatesFundDataStructure() public {
    vm.prank(alice);
    fundMe.fund{value: SEND_VALUE}();
    uint256 amountFunded = fundMe.getAddressToAmountFunded(alice);
    assertEq(amountFunded, SEND_VALUE);

}
```
```ad-note
vm.prank(alice);
模拟当前交易是该alice用户
vm.startPrank(user_address)
vm.stopPrank()

```
```ad-important
new FundMe{value: 1 ether}();
合约的价值，即msg.value
```
### 简化测试用户功能
```c
modifier funded() {
    vm.prank(alice);
    fundMe.fund{value: SEND_VALUE}();
    assert(address(fundMe).balance > 0);
    _;

}
//方便后面测试其他用户功能
function testOnlyOwnerCanWithdraw() public funded {
    vm.expectRevert();
    fundMe.withdraw();

}
```
```c
vm.hoax(address target, uint256 value);
==
vm.deal(user, value); // 分配 ETH
vm.prank(user);      // 切换调用者
```
### 批量生成用户
```c
function testMultipleUsersFund() public {
    uint256 userCount = 5; // 模拟 5 个用户
    uint256 sendValue = 1 ether; // 每个用户存入 1 ETH

    for (uint256 i = 1; i <= userCount; i++) {
        // 生成第 i 个用户的地址（基于私钥的确定性地址）
        address user = vm.addr(i); 

        // 分配 1 ETH 并切换调用者为 user
        vm.hoax(user, sendValue); 

        // 用户调用 fund() 并存入 1 ETH
        fundMe.fund{value: sendValue}();

        // 验证合约中用户的余额是否正确
        assertEq(fundMe.getBalance(user), sendValue);
    }

    // 验证合约总余额是否为 5 ETH
    assertEq(address(fundMe).balance, userCount * sendValue);
}
```
```ad-note
不能从0开始 因为规定address(0)为空地址
```
## 集成 test 工具（devops）
install  integration tests tools
`forge install Cyfrin/foundry-devops --no-commit`
### important thing in foundry.toml
`ffi = true` 允许其执行系统命令
```c
// script
pragma solidity ^0.8.19;

import {Script, console} from "forge-std/Script.sol";
import {FundMe} from "../src/FundMe.sol";
import {DevOpsTools} from "foundry-devops/src/DevOpsTools.sol";

contract FundFundMe is Script {
    uint256 SEND_VALUE = 0.1 ether;

    function fundFundMe(address mostRecentlyDeployed) public {
        vm.startBroadcast();
        FundMe(payable(mostRecentlyDeployed)).fund{value: SEND_VALUE}();
        vm.stopBroadcast();
        console.log("Funded FundMe with %s", SEND_VALUE);
    }

    function run() external {
        address mostRecentlyDeployed = DevOpsTools.get_most_recent_deployment("FundMe", block.chainid);
        fundFundMe(mostRecentlyDeployed);
    }
}
//**test**

pragma solidity 0.8.19;

import {DeployFundMe} from "../../script/DeployFundMe.s.sol";
import {FundFundMe, WithdrawFundMe} from "../../script/Interactions.s.sol";
import {FundMe} from "../../src/FundMe.sol";
import {Test, console} from "forge-std/Test.sol";

contract InteractionsTest is Test {
    FundMe public fundMe;
    DeployFundMe deployFundMe;

    uint256 public constant SEND_VALUE = 0.1 ether;
    uint256 public constant STARTING_USER_BALANCE = 10 ether;

    address alice = makeAddr("alice");


    function setUp() external {
        deployFundMe = new DeployFundMe();
        fundMe = deployFundMe.run();
        vm.deal(alice, STARTING_USER_BALANCE);
    }

    function testUserCanFundAndOwnerWithdraw() public {
        uint256 preUserBalance = address(alice).balance;
        uint256 preOwnerBalance = address(fundMe.getOwner()).balance;

        // Using vm.prank to simulate funding from the USER address
        vm.prank(alice);
        fundMe.fund{value: SEND_VALUE}();

        WithdrawFundMe withdrawFundMe = new WithdrawFundMe();
        withdrawFundMe.withdrawFundMe(address(fundMe));

        uint256 afterUserBalance = address(alice).balance;
        uint256 afterOwnerBalance = address(fundMe.getOwner()).balance;

        assert(address(fundMe).balance == 0);
        assertEq(afterUserBalance + SEND_VALUE, preUserBalance);
        assertEq(preOwnerBalance + SEND_VALUE, afterOwnerBalance);
    }

}
```
```ad-important
`address mostRecentlyDeployed = DevOpsTools.get_most_recent_deployment("FundMe", block.chainid);
        fundFundMe(mostRecentlyDeployed);`
使用这个我们不需要在进行输入难懂的0xafs... 的合同地址
```
## test event logs
```c
import {Vm} from "forge-std/Vm.sol";
//arrange
[......]
//act
// 开始记录日志
vm.recordLogs();
raffle.performUPKeep("");
//获得日志！！！！！Vm V大写了
Vm.Log[] memory logs = vm.getRecordedLogs();
// 0 有特殊含义，所以第一个索引为1
bytes32 requestId = logs[1].topics[1];

// Assert

Raffle.State state = raffle.getState();

console.log("State", uint256(state));

assert(state == Raffle.State.CLOSED);

assert(requestId > 0);

}
```
```ad-note
logs[1].topics[1] 使用时必须保证
event RequestedWinner(uint256 indexed requestId);
有indexd关键词
然后发送emit RequestedWinner(requestId);
才能收到
```
## Fuzz test
```c
function testxxxx(uint256 ID){}
//运行forge test 它会自动爆破ID做fuzz
```
## test zksync 
以下代码如果启用了zksync会报错
```c

import {Test, console} from "forge-std/Test.sol";

import {ZkSyncChainChecker} from "lib/foundry-devops/src/ZkSyncChainChecker.sol";

import {FoundryZkSyncChecker} from "lib/foundry-devops/src/FoundryZkSyncChecker.sol";

  

contract ZkSyncDevOps is Test, ZkSyncChainChecker, FoundryZkSyncChecker {

// Remove the `skipZkSync`, then run `forge test --mt testZkSyncChainFails --zksync` and this will fail!

function testZkSyncChainFails() public skipZkSync {

address ripemd = address(uint160(3));

  

bool success;

// Don't worry about what this "assembly" thing is for now

assembly {

success := call(gas(), ripemd, 0, 0, 0, 0, 0)

}

assert(success);

}

  

// You'll need `ffi=true` in your foundry.toml to run this test

// // Remove the `onlyVanillaFoundry`, then run `foundryup-zksync` and then

// // `forge test --mt testZkSyncFoundryFails --zksync`

// // and this will fail!

// function testZkSyncFoundryFails() public onlyVanillaFoundry {

// bool exists = vm.keyExistsJson('{"hi": "true"}', ".hi");

// assert(exists);

// }

}
```
### **预编译合约地址不同**

在 `testZkSyncChainFails` 测试中，代码尝试调用地址 `0x0000000000000000000000000000000000000003`（即 `ripemd` 预编译合约）：


```c
address ripemd = address(uint160(3)); // 地址 0x3
assembly {
    success := call(gas(), ripemd, 0, 0, 0, 0, 0)
}
```

- ​**在以太坊主网**：地址 `0x3` 是预留给 `RIPEMD-160` 哈希算法的预编译合约，调用会成功。
- ​**在 zkSync 网络**：预编译合约的地址和功能可能与以太坊不同。zkSync 可能未实现 `0x3` 地址的预编译合约，导致调用失败（`success` 为 `false`），从而触发 `assert(success)` 失败。
```ad-note
所以我们需要使用那个工具
 
### --zksync
[ZkSyncChainChecker modifiers:](https://github.com/Cyfrin/foundry-devops?tab=readme-ov-file#zksyncchainchecker-modifiers)

- `skipZkSync`: Skips the function if you are on a zkSync based chain.
- `onlyZkSync`: Only allows the function if you are on a zkSync based chain.
### foundry-zksync
[## FoundryZkSync modifiers:
](https://github.com/Cyfrin/foundry-devops?tab=readme-ov-file#foundryzksync-modifiers)

- `onlyFoundryZkSync`: Only allows the function if you are on `foundry-zksync`
- `onlyVanillaFoundry`: Only allows the function if you are on `foundry`

```

## keccak hash校验
keccak256 函数可以对任意类型的数据（如字符串、字节数组、结构体等）进行哈希计算，返回一个 bytes32 类型的哈希值。
```c
NFT_NAME = "nft";
assert(keccak256(abi.encodePacked(basicNft.name())) == keccak256(abi.encodePacked((NFT_NAME))));
```
# Chisel
interactive terminal of founfry
# Gas
## view Gas
`forge snapshot --mt testWithdrawFromASingleFunder`
now gas of  this function is in  `.gas-snapshot`
but we need know price which is gas \* gas price
```c
// set gas price 
    vm.txGasPrice(GAS_PRICE);
    uint256 gasStart = gasleft();

    [......]

    uint256 gasEnd = gasleft();
    uint256 gasUsed = (gasStart - gasEnd) * tx.gasprice;
    console.log("Withdraw consumed: %d gas", gasUsed);

```
## save gas
### storage
[solidity doc of storage](https://docs.soliditylang.org/en/latest/internals/layout_in_storage.html)
检查storgae布局
`forge inspect <CONTRACT_NAME> storage --pretty`
```ad-note
### 以太坊存储布局核心规则解析

1. ​**存储槽基础结构**
    
    - 每个存储槽（Storage Slot）固定为 ​**32字节（256位）​**，编号从 `0` 开始递增。
    - 示例：合约中第一个变量使用 Slot 0，第二个变量可能用 Slot 1 或与前者共享（根据打包规则）。
2. ​**连续存储与变量打包**
    
    - 变量按声明顺序连续存放，尽可能**合并存储**​（Packing）。
    - ​**打包条件**：变量均为值类型（如 `uintX`、`bytesX`）且总大小 ≤32 字节。
    
    ​**示例1 - 可打包：​**
    
    solidity
    
    ```solidity
    uint8 a;  // 1字节
    uint16 b; // 2字节
    uint24 c; // 3字节
    ```
    
    - 总占用：1 + 2 + 3 = 6字节 → 全部存入 ​**Slot 0**，剩余26字节可用。
    
    ​**示例2 - 不可打包：​**
    
    solidity
    
    ```solidity
    uint8 a;     // Slot 0（1字节）
    uint256 b;   // 32字节 → 必须用 Slot 1
    uint8 c;     // Slot 2（前有32字节变量，无法回填）
    ```
    
3. ​**动态类型的特殊处理**
    
    - ​**映射（Mapping）​**：
        - 声明位置（如 Slot N）不存数据，仅作为计算基址。
        - 键 `k` 对应值的存储位置：`keccak256(h(k) . N)`，`h` 为键处理函数。
    - ​**动态数组（Dynamic Array）​**：
        - 声明位置（如 Slot N）存储数组长度。
        - 元素位置：`keccak256(N) + index`，每个元素按需占用槽位。
    
    ​**示例 - 映射存储计算：​**
    
    solidity
    
    ```solidity
    mapping(address => uint256) balances; // 声明在 Slot 5
    ```
    
    - 地址 `0x123` 的余额存储位置：`keccak256(h(0x123) . 5)`。
4. ​**常量和不可变量优化**
    
    - `constant` 和 `immutable` 变量不占用存储槽，直接嵌入字节码。
    - ​**区别**：
        - `constant`：编译时确定（如 `uint constant X = 10;`）。
        - `immutable`：构造函数中赋值一次（如 `address immutable owner;`）。
5. ​**对齐与跨槽限制**
    
    - 变量必须完整存储在一个槽内，不可跨槽分割。
    - ​**示例**：Slot 0 剩余5字节，下一个 `uint16`（2字节）可存入，但若剩余3字节且下一个是 `uint32`（4字节），则需换到 Slot 1。
```
```ad-tip
so 从内存读数据比从storage读数据要更save gas

```
```c
function cheaperWithdraw() public onlyOwner {
//funderslength 存到了memory
    uint256 fundersLength = s_funders.length;
    for(uint256 funderIndex = 0; funderIndex < fundersLength; funderIndex++) {
[......]
    }
[......]
}
function Withdraw() public onlyOwner {
//  s_funders.length 仍然在storage
    for(uint256 funderIndex = 0; funderIndex < s_funders.length; funderIndex++) {
[......]
    }
[......]
}
```
```ad-important
why
因为在contract中函数外的变量默认为storgae
在函数内的变量默认为memory
```
# 更简单的终端命令
## makefile
```python
-include .env

.PHONY: all test clean deploy fund help install snapshot format anvil zktest

DEFAULT_ANVIL_KEY := 0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80
DEFAULT_ZKSYNC_LOCAL_KEY := 0x7726827caac94a7f9e1b160f7ea819f172f7b6f9d2a97f992c38edeab82d4110

all: clean remove install update build

# Clean the repo
clean  :; forge clean

# Remove modules
remove :; rm -rf .gitmodules && rm -rf .git/modules/* && rm -rf lib && touch .gitmodules && git add . && git commit -m "modules"

install :; forge install cyfrin/foundry-devops@0.2.2 --no-commit && forge install smartcontractkit/chainlink-brownie-contracts@1.1.1 --no-commit && forge install foundry-rs/forge-std@v1.8.2 --no-commit

# Update Dependencies
update:; forge update

build:; forge build

zkbuild :; forge build --zksync

test :; forge test

zktest :; foundryup-zksync && forge test --zksync && foundryup

snapshot :; forge snapshot

format :; forge fmt

anvil :; anvil -m 'test test test test test test test test test test test junk' --steps-tracing --block-time 1

zk-anvil :; npx zksync-cli dev start

deploy:
	@forge script script/DeployFundMe.s.sol:DeployFundMe $(NETWORK_ARGS)

NETWORK_ARGS := --rpc-url http://localhost:8545 --private-key $(DEFAULT_ANVIL_KEY) --broadcast

ifeq ($(findstring --network sepolia,$(ARGS)),--network sepolia)
	NETWORK_ARGS := --rpc-url $(SEPOLIA_RPC_URL) --account $(ACCOUNT) --broadcast --verify --etherscan-api-key $(ETHERSCAN_API_KEY) -vvvv
endif

deploy-sepolia:
	@forge script script/DeployFundMe.s.sol:DeployFundMe $(NETWORK_ARGS)

# As of writing, the Alchemy zkSync RPC URL is not working correctly 
deploy-zk:
	forge create src/FundMe.sol:FundMe --rpc-url http://127.0.0.1:8011 --private-key $(DEFAULT_ZKSYNC_LOCAL_KEY) --constructor-args $(shell forge create test/mock/MockV3Aggregator.sol:MockV3Aggregator --rpc-url http://127.0.0.1:8011 --private-key $(DEFAULT_ZKSYNC_LOCAL_KEY) --constructor-args 8 200000000000 --legacy --zksync | grep "Deployed to:" | awk '{print $$3}') --legacy --zksync

deploy-zk-sepolia:
	forge create src/FundMe.sol:FundMe --rpc-url ${ZKSYNC_SEPOLIA_RPC_URL} --account default --constructor-args 0xfEefF7c3fB57d18C5C6Cdd71e45D2D0b4F9377bF --legacy --zksync


# For deploying Interactions.s.sol:FundFundMe as well as for Interactions.s.sol:WithdrawFundMe we have to include a sender's address `--sender <ADDRESS>`
SENDER_ADDRESS := <sender's address>
 
fund:
	@forge script script/Interactions.s.sol:FundFundMe --sender $(SENDER_ADDRESS) $(NETWORK_ARGS)

withdraw:
	@forge script script/Interactions.s.sol:WithdrawFundMe --sender $(SENDER_ADDRESS) $(NETWORK_ARGS)
```
```ad-note
`include .env`自动加载.env
`.PHONY: build test deploy clean` 伪目标，即不把build当作文件而是命令
`format :; forge fmt` 怎么使用呢：
就是make format === forge fmt
即是make 命令 === 对应makefile里面的命令

DAPP_SRC = ./src   # 定义合约源码目录
DAPP_TEST = ./test # 定义测试目录
通过 `${变量名}` 或 `$(变量名)` 引用。
还可以外部重新赋值

NETWORK = localhost
deploy:; forge script ... --rpc-url http://${NETWORK}:8545

​**使用方式**：`make deploy NETWORK=mainnet`。

*条件判断*
ifeq (${NETWORK}, mainnet)
	RPC_URL = https://mainnet.infura.io/v3/...
else
	RPC_URL = http://localhost:8545
endif

deploy:
	forge script ... --rpc-url ${RPC_URL}

*多命令执行*
all: build test deploy
`make all` === make build& make test & make deploy
```
# 记录事件
```c
event enterPlayer(address indexed player, uint256 ticketPrice);
[......]
emit enterPlayer(msg.sender, i_ticketPrice);
```
```ad-note
indexed 关键词使其更容易被索引
```
外部应用监听事件 假设你有一个前端DApp使用Web3.js或Ethers.js与智能合约交互，你可以监听 UserRegistered 事件，如下所示（以Web3.js为例）： 
```js
const contract = new web3.eth.Contract(abi, contractAddress);

contract.events.UserRegistered({
    fromBlock: 0
}, function(event) {
    console.log(`用户注册: 地址=${event.returnValues.user}, 名称=${event.returnValues.name}, 时间=${event.returnValues.timestamp}`);
})
.on('data', function(event) {
    // 处理实时事件
})
.on('error', console.error);
```
监听事件 ：通过 contract.events.UserRegistered 方法监听 UserRegistered 事件。 回调函数 ：当事件被触发时，回调函数会接收到事件对象 event ，其中包含事件的返回值（如用户地址、名称和时间戳）。 实时监听 ：通过处理 'data' 事件，可以实时响应合约中的用户注册操作。
# 使用随机数
 [vrf-supported-network](https://docs.chain.link/vrf/v2-5/supported-networks)
## 在网络上使用随机数

```c
import {IVRFCoordinatorV2Plus} from "@chainlink/contracts/src/v0.8/vrf/dev/interfaces/IVRFCoordinatorV2Plus.sol";
import {VRFV2PlusClient} from "@chainlink/contracts/src/v0.8/vrf/dev/libraries/VRFV2PlusClient.sol";
import {VRFCoordinatorV2_5} from "@chainlink/contracts/src/v0.8/vrf/dev/VRFCoordinatorV2_5.sol";
import {VRFConsumerBaseV2Plus} from "@chainlink/contracts/src/v0.8/vrf/dev/VRFConsumerBaseV2Plus.sol";
import {AutomationCompatibleInterface} from "@chainlink/contracts/src/v0.8/automation/interfaces/AutomationCompatibleInterface.sol";
//声明继承关系
contract xxx is VRFConsumerBaseV2Plus,AutomationCompatibleInterface{
// Chainlink VRF related variables
//去标准文档找到对应网路的address
IVRFCoordinatorV2Plus private immutable i_vrfCoordinatorContract;
uint256 private immutable i_subscriptionId;
bytes32 private immutable i_gasLane;
uint32 private immutable i_callbackGasLimit;
uint16 private constant REQUEST_CONFIRMATIONS = 3;
uint32 private constant NUM_WORDS = 1;

constructor(

[......]

uint256 subscriptionId,
bytes32 gasLane, // keyHash
uint32 callbackGasLimit,
address vrfCoordinatorV2
//声明继承构造函数

) VRFConsumerBaseV2Plus(vrfCoordinatorV2) {
[......]
i_vrfCoordinatorContract = IVRFCoordinatorV2Plus(vrfCoordinatorV2);
i_subscriptionId = subscriptionId;
i_gasLane = gasLane;
i_callbackGasLimit = callbackGasLimit;

}
function checkUpkeep() public view{

}
function performUpkeep() external{
//请求随机数
uint256 requestId = s_vrfCoordinator.requestRandomWords(
VRFV2PlusClient.RandomWordsRequest({
keyHash: i_gasLane,
subId: i_subscriptionId,
requestConfirmations: REQUEST_CONFIRMATIONS,
callbackGasLimit: i_callbackGasLimit,
numWords: NUM_WORDS,
extraArgs: VRFV2PlusClient._argsToBytes(
// Set nativePayment to true to pay for VRF requests with Sepolia ETH instead of LINK
VRFV2PlusClient.ExtraArgsV1({nativePayment: false})
)
})
);

}
//处理随机数
function fulfillRandomWords(uint256 requestId, uint256[] calldata randomWords) internal override {
uint256 indexOfWinner = randomWords[0] % s_players.length;

address payable winner = s_players[indexOfWinner];

s_recentWinner = winner;

(bool success,) = winner.call{value:address(this).balance}("");

if (!success) {

revert ();

}
```
```ad-note
如果出现should be absract contract报错
是因为没有覆盖checkUpkeep和performUpkeep（AutomationCompatibleInterface）
```
## local mock  随机数
```c
import {VRFCoordinatorV2_5Mock} from "chainlink/src/v0.8/vrf/mocks/VRFCoordinatorV2_5Mock.sol";

/* VRF Mock Values */
uint96 public constant MOCK_BASE_FEE = 0.25 ether;
uint96 public constant MOCK_GAS_PRICE_LINK = 1e9;
int256 public constant MOCK_WEI_PER_UNIT_LINK = 4e15;

vm.startBroadcast();
VRFCoordinatorV2_5Mock vrfCoordinatorMock = new VRFCoordinatorV2_5Mock(
    MOCK_BASE_FEE,
    MOCK_GAS_PRICE_LINK,
    MOCK_WEI_PER_UNIT_LINK,
);

vm.stopBroadcast();
```
## 部署随机数
### 完整操作流程总结 
- 创建订阅 ID ：调用 createSubscription 。 
- 充值 LINK ：向订阅 ID 转账 LINK。 
- 注册消费者 ：将合约添加到订阅的消费者列表。 
- 请求随机数 ：使用 requestRandomWords 触发 VRF 请求。 
通过订阅模型，Foundry 项目可以高效管理 Chainlink VRF 的随机数请求，适用于抽奖、NFT 铸造等场景。
### 部署到sepolia
[https://vrf.chain.link/sepolia](https://vrf.chain.link/sepolia)
```ad-important
从上面的创建订阅并且修改helpconfig配置中的订阅id，然后获取link，冲进去，然后添加消费者，都可以在网站UI进行
```
### 部署分叉链如sepolia的注意
#以某个私钥账户广播
`vm.startBroadcast(privateKey)`
### 部署到本地
```c
//DeployRaffle.s.sol
if (config.subscriptionId == 0) {
// 创建订阅
CreateSubscription createSubscription = new CreateSubscription();
(config.subscriptionId, config.vrfCoordinatorV2) = createSubscription.createSubscriptionUsingConfig();
//发送link
FundSubscription fundSubscription = new FundSubscription();
fundSubscription.fundSubscription(config.vrfCoordinatorV2, config.subscriptionId, config.link);
}
[......]
//添加消费者
AddConsumer addConsumer = new AddConsumer();
addConsumer.addConsumer(address(raffle),config.vrfCoordinatorV2,config.subscriptionId);
```
#### 创建本地订阅
```c
contract CreateSubscription is Script {

function createSubscriptionUsingConfig() public returns (uint256, address) {

HelpConfig helpConfig = new HelpConfig();

HelpConfig.NetworkConfig memory config = helpConfig.getConfig();

address vrfCoordinator = config.vrfCoordinatorV2;

(uint256 subId,) = createSubscription(vrfCoordinator);

return (subId, vrfCoordinator);

}

  

function createSubscription(address vrfCoordinator) public returns (uint256, address) {

console.log("Creating subscription on chainId", block.chainid);

vm.startBroadcast();

uint256 subscriptionId = VRFCoordinatorV2_5Mock(vrfCoordinator).createSubscription();

vm.stopBroadcast();

console.log("Subscription created with id", subscriptionId);

console.log("Updating config with subscriptionId", subscriptionId);

return (subscriptionId, vrfCoordinator);

}

  

function run() public {

createSubscriptionUsingConfig();

}

}
```
#### 充值本地link
```c
contract FundSubscription is Script, CodeConstants {

uint256 public constant FUND_AMOUNT = 3 ether; //3 link

  

function fundSubscriptionUsingConfig() public {

HelpConfig helpConfig = new HelpConfig();

HelpConfig.NetworkConfig memory config = helpConfig.getConfig();

address vrfCoordinator = config.vrfCoordinatorV2;

uint256 subId = config.subscriptionId;

console.log("Funding subscription with id", subId);

address link = config.link;

fundSubscription(vrfCoordinator, subId, link);

}

  

function fundSubscription(address vrfCoordinator, uint256 subId, address link) public {

console.log("Funding subscription on chainId", block.chainid);

  

if (block.chainid == LOCAL_CHAIN_ID) {

vm.startBroadcast();

VRFCoordinatorV2_5Mock(vrfCoordinator).fundSubscription(subId, FUND_AMOUNT);

vm.stopBroadcast();

} else {

vm.startBroadcast();

LinkToken(link).transferAndCall(vrfCoordinator, FUND_AMOUNT, abi.encode(subId));

vm.stopBroadcast();

}

}

  

function run() public {

fundSubscriptionUsingConfig();

}

}
```
#### 添加本地消费者
```c
contract AddConsumer is Script{

function addConsumerUsingConfig(address RecentlyDeployed) public {

HelpConfig helpConfig = new HelpConfig();

HelpConfig.NetworkConfig memory config = helpConfig.getConfig();

address vrfCoordinator = config.vrfCoordinatorV2;

uint256 subId = config.subscriptionId;

console.log("Adding consumer with subscription id", subId);

addConsumer(RecentlyDeployed,vrfCoordinator, subId);

}

  

function addConsumer(address RecentlyDeployed, address vrfCoordinator, uint256 subId) public {

console.log("Adding consumer on chainId", block.chainid);

vm.startBroadcast();

VRFCoordinatorV2_5Mock(vrfCoordinator).addConsumer(subId, RecentlyDeployed);

vm.stopBroadcast();

}

  

function run() external {

address RecentlyDeployed = DevOpsTools.get_most_recent_deployment("Raffle",block.chainid);

addConsumerUsingConfig(RecentlyDeployed);

}

}
```
### 获取LINK
#### sepolia直接查询文档
#### 本地部署link
```c
//linktoken.sol
// SPDX-License-Identifier: MIT

// @dev This contract has been adapted to fit with foundry

pragma solidity ^0.8.0;

  

import {ERC20} from "@solmate/tokens/ERC20.sol";

  

interface ERC677Receiver {

function onTokenTransfer(address _sender, uint256 _value, bytes memory _data) external;

}

  

contract LinkToken is ERC20 {

uint256 constant INITIAL_SUPPLY = 1000000000000000000000000;

uint8 constant DECIMALS = 18;

  

constructor() ERC20("LinkToken", "LINK", DECIMALS) {

_mint(msg.sender, INITIAL_SUPPLY);

}


function mint(address to, uint256 value) public {

_mint(to, value);

}

event Transfer(address indexed from, address indexed to, uint256 value, bytes data);

  

/**

* @dev transfer token to a contract address with additional data if the recipient is a contact.

* @param _to The address to transfer to.

* @param _value The amount to be transferred.

* @param _data The extra data to be passed to the receiving contract.

*/

function transferAndCall(address _to, uint256 _value, bytes memory _data) public virtual returns (bool success) {

super.transfer(_to, _value);

// emit Transfer(msg.sender, _to, _value, _data);

emit Transfer(msg.sender, _to, _value, _data);

if (isContract(_to)) {

contractFallback(_to, _value, _data);

}

return true;

}


// PRIVATE

function contractFallback(address _to, uint256 _value, bytes memory _data) private {

ERC677Receiver receiver = ERC677Receiver(_to);

receiver.onTokenTransfer(msg.sender, _value, _data);

}

  

function isContract(address _addr) private view returns (bool hasCode) {

uint256 length;

assembly {

length := extcodesize(_addr)

}

return length > 0;

}

}
```
#### 在config添加LINK
`link: address(new LinkToken)`

# 链式自动化
## 两个过程
`检查是否可以自动化了。 checkUPKeep
`开始执行 preformUPKeep`
```c
function checkUpKeep(bytes calldata/*checkdata */)
public
view
returns (bool upkeepNeeded, bytes memory )

{

bool timePassed = block.timestamp > lastTime + i_interval;

bool isBalance = address(this).balance > 0;

bool hasPlayers = s_players.length > 0;

bool isOpen = s_state == State.OPEN;

return (timePassed && isBalance && hasPlayers && isOpen, "");

}
```
```c
//pickwinner 修改为
function performUPKeep(bytes calldata /* performData */ ) external {

[......]
(bool upkeepNeeded,) = checkUpKeep("");

if (!upkeepNeeded) {

revert Raffle_notupkeep(address(this).balance,s_players.length,uint256(s_state));

}
[......]
```
# More Cheatcode
- [vm.warp](https://book.getfoundry.sh/cheatcodes/warp?highlight=warp#warp) which sets the `block.timestamp`;
    
- [vm.roll](https://book.getfoundry.sh/cheatcodes/roll?highlight=roll#roll) which sets the `block.number`;
# foundry.toml
```json
[profile.default]
src = "src"
out = "out"
libs = ["lib"]
fs_permissions = [
    { access = "read", path = "./images/" },
    { access = "read", path = "./broadcast" },
]
ffi = true

remappings = ['@openzeppelin/contracts=lib/openzeppelin-contracts/contracts']

[etherscan]
mainnet = { key = "${ETHERSCAN_API_KEY}" }
sepolia = { key = "${ETHERSCAN_API_KEY}" }

[rpc_endpoints]
sepolia = "${SEPOLIA_RPC_URL}"

# See more config options https://github.com/foundry-rs/foundry/tree/master/config

```