## JDBC注入
### Sink寻找

1. 查找所有方法调用
```Sql
import java
from MethodCall call
select call
```
![](media/Pasted%20image%2020250512204911.png)  
2. 拿到每个方法调用所调用的方法
```Java
import java
from MethodCall call,Method m
where call.getMethod()=m
select call,m
```
![](media/Pasted%20image%2020250512205127.png)  
3. 找到Sql注入漏洞常见方法名使用的方法
```Sql
import java
from MethodCall call,Method method 
where call.getMethod() = method and
    method.getName()="executeQuery"
select call,method
```
![](media/Pasted%20image%2020250512205720.png)  
4. ~~限定类名~~(改变了以下条件判断：==查找所有在 java.sql.Statement 或其子类型上调用的、且方法名包含 "execute" 的方法调用==)
```Sql
import java


from MethodCall call, Method calledMethod
where
    // 条件 1: 获取 'call' 实际调用的方法，赋值给 calledMethod
    
    calledMethod = call.getMethod() and

    
    // 条件 2: 检查被调用方法 calledMethod 的名称是否包含子字符串 "execute"
    // .getName() 获取方法名的字符串
    // .indexOf("execute") 查找子字符串 "execute" 的位置，如果找不到返回 -1
    // >= 0 表示找到了该子字符串
    
    calledMethod.getName().indexOf("execute") >= 0 and // <- 修正了 >= 0and


    // 条件 3: 检查声明 calledMethod 的类型是否为 java.sql.Statement 或其任何子类型/实现类
    // .getDeclaringType() 获取声明该方法的类或接口 (例如 java.sql.Statement)
    // .getASourceSupertype*() 获取该类型的所有父类和实现的接口 (包括自身)
    // .hasQualifiedName(...) 检查这些类型中是否有 java.sql.Statement
    
    calledMethod.getDeclaringType().getASourceSupertype*().hasQualifiedName("java.sql", "Statement")


select call, calledMethod
```
5. 查找方法的参数
	1. `getArgument(int index)` 获取方法调用的指定位置上的参数(从0开始)，返回值为 Expr
```Sql
import java

from MethodCall call,Expr p
where
    call.getMethod().getName().indexOf("execute") >= 0 and

    call.getMethod().getDeclaringType().getASourceSupertype*().hasQualifiedName("java.sql", "Statement") and

    p = call.getArgument(0)
select call, p

```
![](media/Pasted%20image%2020250512211005.png)
6. 封装成谓词
```Sql
import java

predicate isJdbcSqlDeserialized(Expr arg) {
    exists(MethodCall sqlExec | 
        sqlExec.getMethod().getName().indexOf("execute") >= 0 and
        sqlExec.getMethod().getDeclaringType().getASourceSupertype*().hasQualifiedName("java.sql", "Statement") and
        arg = sqlExec.getArgument(0)
        )    
}

from Expr arg
where isJdbcSqlDeserialized(arg)
select arg
```
### Source
1. 找到所有的参数
```Sql
import java
from Parameter p
select p
```
2. 找到所有公共方法或公开构造函数的参数
```Sql
import java

from Parameter p
where
    
    // 添加条件：参数 p 所属的 Callable (方法或构造函数) 必须是公共的
    // p.getCallable() 获取参数 p 所属的方法或构造函数
    // .isPublic() 检查这个方法或构造函数是否声明为 public
    
    p.getCallable().isPublic()


select p
```
3. ~~通过常见的web 包名 过滤参数~~
```Sql
import java

// 第四步 (启发式): 查找特定包或类名模式下的公共方法参数

from Parameter p, Method m // 只关注方法的参数，排除构造函数参数简化逻辑
where
    // 条件 1: 参数 p 属于方法 m
    p.getCallable() = m and

    // 条件 2: 方法 m 是公共的
    m.isPublic() and

    // 条件 3: (启发式) 检查方法 m 所属的类满足特定模式
    exists(Class declaringClass |
        declaringClass = m.getDeclaringType().getASourceSupertype*() and // 获取声明方法的类
        (
            // 模式 A: 包名包含 controller, web, api, 或 servlet (修正后的匹配)
            (
                declaringClass.getPackage().getName().matches("%.controller.%") or
                declaringClass.getPackage().getName().matches("%.web.%") or
                declaringClass.getPackage().getName().matches("%.api.%") or
                declaringClass.getPackage().getName().matches("%.servlet.%")
            ) or // 连接包名模式和类名模式
            // 模式 B: 类名以 Controller, Servlet, Resource, Handler, Endpoint 结尾
            declaringClass.getName().regexpMatch(".*(Controller|Servlet|Resource|Handler|Endpoint)$")
        )
    )

// 选择满足启发式条件的参数 p
select p, m.getDeclaringType().getASourceSupertype*().getPackage().getName() // 同时显示参数和其所属的方法，方便确认
```
4. 封装成谓词
```Sql
import java


predicate isHeuristicUserInputSource(Parameter p) {
    // 检查是否存在这样一个方法 m
    exists(Method m |
        // 条件 1: 参数 p 属于方法 m
        p.getCallable() = m and
        // 条件 2: 方法 m 是公共的
        m.isPublic() and
        // 条件 3: (启发式) 检查方法 m 所属的类 (或其父类/接口) 满足特定模式
        exists(Class declaringClassOrSupertype |
            // declaringClassOrSupertype 是声明 m 的类，或者是该类的父类/接口
            // 注意：您在原始查询中使用了 .getASourceSupertype*()，我们在这里保留它
            declaringClassOrSupertype = m.getDeclaringType().getASourceSupertype*() and
            (
                // 模式 A: 包名匹配
                (
                    declaringClassOrSupertype.getPackage().getName().matches("%.controller.%") or
                    declaringClassOrSupertype.getPackage().getName().matches("%.web.%") or
                    declaringClassOrSupertype.getPackage().getName().matches("%.api.%") or
                    declaringClassOrSupertype.getPackage().getName().matches("%.servlet.%")
                ) or // 连接包名模式和类名模式
                // 模式 B: 类名匹配
                declaringClassOrSupertype.getName().regexpMatch(".*(Controller|Servlet|Resource|Handler|Endpoint)$")
            )
        )
    )
}

// --- 如何使用这个谓词 ---

// 示例：查找所有满足条件的源参数
from Parameter p
where isHeuristicUserInputSource(p) // 直接调用我们定义的谓词
select p, p.getCallable() // 选择参数和它所属的 Callable (方法)

```

### 数据流配置
- 要先导入两个
```Sql
import java
import semmle.code.java.dataflow.DataFlow
import semmle.code.java.dataflow.TaintTracking
```
- https://github.blog/changelog/2023-08-14-new-dataflow-api-for-writing-custom-codeql-queries/
```Sql
module JdbcTaintConfig implements DataFlow::ConfigSig {
  predicate isSource(DataFlow::Node source) {
    exists(Parameter p |
      source.asParameter() = p and
      isHeuristicSource(p)  // 直接调用全局谓词
    )
  }

  predicate isSink(DataFlow::Node sink) {
    exists(Expr arg, MethodCall call |
      sink.asExpr() = arg and
      isExecuteMethod(call) and  // 直接调用全局谓词
      arg = call.getArgument(0)
    )
  }
}

/************************ 执行部分 ************************/
module JdbcFlow = TaintTracking::Global<JdbcTaintConfig>;
import JdbcFlow::PathGraph

from JdbcFlow::PathNode source, JdbcFlow::PathNode sink
where JdbcFlow::flowPath(source, sink)
select sink.getNode(), source, sink, "用户输入 $@ 流向SQL执行点",
 source.getNode(), "来自Controller参数"
```
- 没有定义Barrier ，误报严重（数据流分析被改版了---==2024.12之后== ）
![](media/Pasted%20image%2020250513011609.png)  



## Rce
1. Source谓词
```Java
predicate isParametersoure(Parameter p){
	与上面的一样
}
```
2. Sink谓词
```Java
 predicate isRceSink(MethodCall call){
  exists( Method m | m=call.getMethod() and m.getName()="exec" and  m.getDeclaringType().hasQualifiedName("java.lang","Runtime") ) and
  call.getMethod().getNumberOfParameters() >=1 and
    (
    exists(Type argType | argType = call.getArgument(0).getType() and argType.toString() = "String[]" or argType.toString() ="String")
    )
}
```
3. 数据流
```Java
module RceFlowConfig implements DataFlow::ConfigSig {
  predicate isSource(DataFlow::Node source) {
    exists(Parameter p |
      source.asParameter() = p and
      isParameterSource(p)  // 直接调用全局谓词
    )
  }

  

  predicate isSink(DataFlow::Node sink) {
    exists(Expr arg, MethodCall call |
      sink.asExpr() = arg and
      isRceSink(call) and  // 直接调用全局谓词
      arg = call.getArgument(0)
    )
  }
}

module RceFlow = TaintTracking::Global<RceFlowConfig>;
import RceFlow::PathGraph

  

from RceFlow::PathNode source, RceFlow::PathNode sink
where RceFlow::flowPath(source, sink)
select sink.getNode(), source, sink,"RunTimeExecRce漏洞"
```


